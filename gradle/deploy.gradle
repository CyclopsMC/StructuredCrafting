apply plugin: 'maven'

curseforge {
    if(project.hasProperty("curseforge_key") && project.hasProperty("changelog")) {
        apiKey = project.curseforge_key
    } else if(System.getenv().TRAVIS) {
        apiKey = System.getenv().CURSEFORGE_KEY_SECRET
    } else {
        apiKey = "DUMMY"
    }

    project {
        id = "233151" // my project url is http://minecraft.curseforge.com/mc-mods/233151/
        releaseType = project.config.release_type
        mainArtifact(jar) {
            relations {
                requiredLibrary 'cyclops-core'
            }
        }

        changelog = ""
        if (new File("resources/changelog/${project.version}.txt").exists()) {
            changelog = new File("resources/changelog/${project.version}.txt").text
        }

        addGameVersion '1.10'
        addGameVersion '1.10.2'

        addArtifact deobfJar
        addArtifact sourceJar
        addArtifact javadocJar
    }
}

configurations {
    deployerJars
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-ftp:2.2"
}

uploadArchives {
    repositories {
        add getProject().repositories.mavenLocal()
    }
    repositories.mavenDeployer {
        configuration = configurations.deployerJars

        if (project.hasProperty("filesmaven_url")) {
            logger.info('Publishing to files server')
            repository(url: project.filesmaven_url) {
                authentication(userName: project.filesmaven_username, password: project.filesmaven_key)
            }
        } else if (System.getenv().MAVEN_URL) {
            logger.info('Publishing to files server')
            repository(url: System.getenv().MAVEN_URL) {
                authentication(userName: System.getenv().MAVEN_USERNAME, password: System.getenv().MAVEN_KEY)
            }
        } else {
            logger.info('Publishing to repo folder')
            repository(url: 'file://localhost/' + project.file('~/.m2/repository').getAbsolutePath())
        }

        pom {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName
        }
        pom.project {
            name project.archivesBaseName
            packaging 'jar'
            description 'Craft stuff in your world, automatically.'
            url 'https://github.com/CyclopsMC/StructuredCrafting'

            scm {
                url 'https://github.com/CyclopsMC/StructuredCrafting'
                connection 'scm:git:git://github.com/CyclopsMC/StructuredCrafting.git'
                developerConnection 'scm:git:git@github.com:CyclopsMC/StructuredCrafting.git'
            }

            issueManagement {
                system 'github'
                url 'https://github.com/CyclopsMC/StructuredCrafting/issues'
            }

            developers {
                developer {
                    id 'rubensworks'
                    name 'rubensworks'
                    roles { role 'developer' }
                }
            }
        }
    }
}
